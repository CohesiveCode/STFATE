

      SUBROUTINE MAIN(X,Z,DEPTH,ICODE,CP,COUT,THICKP,TOPP,SUM,C,THICK,
     1 TOP,ACCUM,U,W,SS,TSIDE,TTHK,TTOP,TMASS,TX,TZ,NMAX,MMAX)
C
C     MAIN PROGRAM
C
         COMMON/DIMEN/ NS,NSP1,NVL,NSC
         DIMENSION COUT(NMAX,1),SUM(NMAX,1),C(NMAX,1),THICK(NMAX,1),
     1    TOP(NMAX,1),ACCUM(NMAX,1),X(NMAX,1),Z(NMAX,1),DEPTH(NMAX,1),
     2    ICODE(NMAX,1),CP(NMAX,1),THICKP(NMAX,1),TOPP(NMAX,1),
     3    U(NMAX,MMAX,1),W(NMAX,MMAX,1),SS(600,NS),TSIDE(1),TTHK(1),
     4    TTOP(1),TMASS(1),TX(1),TZ(1)
         COMMON/DPASS/ NPASS,MPASS
         COMMON/BAY/ DX,DTL,XBARGE,ZBARGE,DXH,DXR,AREA
         COMMON/GUIDE1/ TDUMP,TSTOP,ISTEP,IPLUNG,NUTRL,NTRIAL,ILEAVE,
     1    KEY1,KEY2,KEY3
         COMMON/GUIDE2/ NIND,NLINE(150),MF(150),ML(150)
         COMMON/AMB/ NROA,IY,Y(10),ROA(10),H
         COMMON/CLOUD/ T(600),CX(600),CY(600),CZ(600),CU(600),CV(600),
     1    CW(600),DENDIF(600),BC(600),AA(600),FC(600),VF
         COMMON/PIECES/ PARAM(13),ROAS(13),CS(13),VFALL(13),VOIDS(13),
     1    BVOID
         COMMON/GPI/ G,PI
         COMMON/CHECK/ TOTAL
         COMMON/LOST/ GONE
         COMMON/USERDT/ KEY4,DT1U,DT2U
         COMMON/GP/ IGCN,IGCL,IGLT,IPCN,IPCL,IPLT
         COMMON/FLEE/ ITD,TD(6),DC(6),TRACER,CINIT,CBACK
         COMMON/P/ PRT
         LOGICAL PRT
         DIMENSION ID(8),TPRT(12)
C
         DATA G,PI /32.2,3.14159/
C     NON-ANSI
         DATA TPRT/12*0./
C     NON-ANSI
         DATA ITD/1/,(DC(I),I=1,6)/.1,.01,.001,.0001,.00001,1.E-30/
C     NON-ANSI
C
         NPASS = NMAX
         MPASS = MMAX
         CALL SECOND(T1)
         WRITE(6,5)
C
C
    5    FORMAT(1H1///10X,59HFATE OF DREDGED MATERIAL DEPOSITED IN AN ESTUA
     1   RY BY DUMPING
     2   )
C     READ EXECUTION MANAGEMENT PARAMETERS
         READ(5,15) KEY1,KEY2,KEY3,KEY4
         READ(5,15) IGCN,IGCL,IPCN,IPCL,IPLT
   15    FORMAT(16I5)
         WRITE(6,25) KEY1,KEY2,KEY3,KEY4,IGCN,IGCL,IPCN,IPCL,IPLT
   25    FORMAT(//10X,30HEXECUTION PARAMETERS FOLLOW.../10X,
     1    52HKEY1 KEY2 KEY3 KEY4 IGCN IGCL IPCN IPCL IPLT/7X,10I6)

C     READ ALPHAMERIC IDENTIFICATION FOR THIS RUN
         READ(5,35) ID
   35    FORMAT(8A10)
         WRITE(6,105) ID
  105    FORMAT(8(/),10X,8A10,////)

C     DEFINE ESTUARY GEOMETRY AND ARRAYS GOVERNING LONG TERM COMPUTATION
         CALL ESTGEO(DEPTH,ICODE,NMAX,MMAX)
C     READ DUMP LOCATION COORDINATES AND DENSITY STRUCTURE ...
C     ...ALSO NUMBER OF VELOCITY LAYERS AND LOG PROFILE INDICATOR
         CALL AMBC(DEPTH,NMAX)
C     READ TIME OF DUMP (W/R TO START OF TIDAL CYCLE), DURATION OF
C     SIMULATION, AND TIME STEP IN LONG TERM
         READ(5,45) TDUMP,TSTOP,DTL
   45    FORMAT(8E10.0)
         WRITE(6,125) TDUMP,TSTOP,DTL
  125    FORMAT(///10X,25HTIME PARAMETERS FOLLOW.../10X,15HTIME OF DUMP
     1   ,F10.2,35H SECONDS AFTER START OF TIDAL CYCLE/10X,
     2    25HDURATION OF SIMULATION,F10.2,19H SECONDS AFTER DUMP/10X,
     3    28HLONG TERM TIME STEP (DTL),F10.2,8H SECONDS,6(/))
         IF(KEY4.EQ.0) GOTO 124
         READ(5,45) DT1U,DT2U
         WRITE(6,119) DT1U,DT2U
  119    FORMAT(/////10X,40HINTEGRATION TIME STEPS SPECIFIED BY USER /10X
     1   ,14HIN DUMP, DT=,G14.5,5X,16HIN COLLAPSE, DT=,G14.5)
  124    CONTINUE
C
C
C     SET PRINTING TIMES ACCORDING TO IPLT
         IF(IPLT) 150,150,170
C
C
C     HERE TO SET DEFAULT PRINTING TIMES
  150    DTP = TSTOP/4.
         INC = DTP/DTL + .0001
         IF(INC.LT.1) INC=1
         DO 160 I=1,4
            DTP = FLOAT(INC)*DTL
  160    TPRT(I) = FLOAT(I)*DTP
         IF(TPRT(4).GT.TSTOP) TPRT(4)=TSTOP
         GOTO 180
C     HERE TO SET USER SPECIFIED PRINTING TIMES
  170    CONTINUE
         READ(5,45) (TPRT(I),I=1,IPLT)
C     READ INITIAL VELOCITY FIELD
  180    CALL UW(0.,U,W,NMAX,MMAX)
C     CONVECTIVE DESCENT...
         CALL DUMP(SS,U,W,DEPTH,NS)
         IND = NUTRL + IPLUNG
         IF(NTRIAL.EQ.5.AND.IND.EQ.0) GOTO 700
         IF(KEY2.EQ.1) GOTO 800
         IF(IPLUNG.EQ.1) GOTO 250
C     CHECK DENSITY GRADIENT AT CLOUD LOCATION
         NN=NROA-1
         DO 200 I=1,NN
            IF(CY(ISTEP).GE.Y(I).AND.CY(ISTEP).LE.Y(I+1)) DENGRA =
     1       (ROA(I+1)-ROA(I))/(Y(I+1)-Y(I))
  200    CONTINUE
         IF(DENGRA.GT.1.0E-10) GOTO 250
         WRITE(6,205)
  205    FORMAT(1H1,10X,56HDENSITY GRADIENT = 0, GO DIRECTLY TO LONG TERM
     1    DIFFUSION )
C     ....IF KEY2=2, TERMINATE COMPUTATION....
         IF(KEY2.EQ.2) GOTO 800
         GOTO 250
C     DYNAMIC COLLAPSE...
  250    CALL COLAPS(SS,U,W,DEPTH,NS)
         IF(NTRIAL.EQ.5.AND.NUTRL.NE.3) GOTO 700
         IF(KEY2.EQ.2) GOTO 800
C     LONG TERM DIFFUSION FOLLOWS.....
C     DETERMINE NUMBER OF COMPLETE TIDAL CYCLES AND FRACTION OF LAST
C     TIDAL CYCLE TO RUN
         TSUM = (TDUMP+TSTOP)/3600.
         NCYCLE = TSUM/25. + .0001
         XS = TDUMP+TSTOP - 25.*3600.*FLOAT(NCYCLE)
         IF(NCYCLE.EQ.0) NCYCLE=1
C
C
C     CLEAR SUM OF BOTTOM ACCUMULATION
         DO 260 M=1,MMAX
            DO 260 N=1,NMAX
  260    SUM(N,M)=0.
C
C     LOOP ON COMPONENTS
         DO 400 K=1,NSP1
C
C
C
C
C
            GONE=0.
            ETS=0.
            IF(K.EQ.NSP1.AND.KEY3.EQ.0) GOTO 400
            WRITE(6,265) PARAM(K)
  265       FORMAT(1H1//10X,38HBEGIN LONG TERM SIMULATION OF FATE OF ,A10)
C     CLEAR ARRAYS
            DO 270 M=1,MMAX
               DO 270 N=1,NMAX
                  C(N,M)=0.
                  THICK(N,M)=0.
                  TOP(N,M)=0.
                  ACCUM(N,M)=0.
  270       CONTINUE
C     DO BOOKKEEPING FOR MASS TRANSFER FROM SHORT TERM TO LONG TERM
            CALL BOOKS(K,SS,TSIDE,TTHK,TTOP,TMASS,TX,TZ,NS,DEPTH,ACCUM,U,W,
     1       NMAX,MMAX,NSC)
            INDPRT=1
            DO 300 ICYCLE=1,NCYCLE
               IFST=1
               ILST = 25.*3600./DTL + .0001
               IF(ICYCLE.EQ.1) IFST = TDUMP/DTL + .0001
               IF(IFST.LT.1) IFST=1
               IF(ICYCLE.EQ.NCYCLE) ILST = XS/DTL + .0001
               DO 300 IDTL=IFST,ILST
C     ETS IS ELAPSED TIME FROM DUMP (IN SECONDS)
C     UPDATE VELOCITIES
                  ETS = ETS+DTL
                  CALL UW(ETS,U,W,NMAX,MMAX)
C     SET PRINT INDICATOR PRT
                  PRT = .FALSE.
                  IF(ABS(ETS-TPRT(INDPRT)).GT..01) GOTO 280
                  PRT = .TRUE.
                  INDPRT = INDPRT+1
  280             CONTINUE
C     CALL ROUTINE TO MOVE AND DIFFUSE CLOUDS
                  CALL MAD(K,ETS,X,Z,U,W,C,THICK,TOP,DEPTH,ACCUM,CP,THICKP,TOPP,
     1             COUT,ICODE,TSIDE,TTHK,TTOP,TMASS,TX,TZ,NMAX,MMAX)
                  IF(TOTAL.LT.1.0E-06) GOTO 310
  300       CONTINUE
C
C
C
            GOTO 330
  310       WRITE(6,315) PARAM(K),ETS
  315       FORMAT(///18H COMPUTATIONS FOR ,A10,15H TERMINATED AT,F10.2,
     1       48H SEC. ELAPSED TIME... MATERIAL SETTLED TO BOTTOM)
  330       CONTINUE
            IF(K.EQ.NSP1) GOTO 400
C     ....HERE SAVE TOTAL ACCUMULATION (ALL COMPONENTS)
            DO 340 M=1,MMAX
               DO 340 N=1,NMAX
  340       SUM(N,M)=SUM(N,M)+ACCUM(N,M)
C
C     PRINT FINAL ACCUMULATED VOLUME OF THIS COMPONENT
C
            DO 350 M=1,MMAX
               DO 350 N=1,NMAX
  350       COUT(N,M)=ACCUM(N,M)
            CALL PRINTC(COUT,NMAX,MMAX,PARAM(K),ETS,2,ICODE)
C     PRINT FINAL THICKNESS ACCUMULATION FOR THIS COMPONENT
            C1 = (1.+VOIDS(K))/AREA
            DO 360 M=1,MMAX
               DO 360 N=1,NMAX
  360       COUT(N,M)=ACCUM(N,M)*C1
            CALL PRINTC(COUT,NMAX,MMAX,PARAM(K),ETS,5,ICODE)
  400    CONTINUE
C
C
C     ...HERE PRINT TOTAL SOLID VOLUME (ALL COMPONENTS) ACCUMULATED
         WRITE(6,405)
  405    FORMAT(1H1,/////,10X,57HFINAL DISTRIBUTIONS OF TOTAL SETTLED MATER
     1   IAL FOLLOW.....
     2   )
         DO 410 M=1,MMAX
C
C
            DO 410 N=1,NMAX
  410    COUT(N,M) = SUM(N,M)
         CALL PRINTC(COUT,NMAX,MMAX,PARAM(K),ETS,6,ICODE)
         C1 = (1.+BVOID)/AREA
         DO 440 M=1,MMAX
            DO 440 N=1,NMAX
  440    COUT(N,M) = SUM(N,M)*C1
         CALL PRINTC(COUT,NMAX,MMAX,PARAM(K),ETS,7,ICODE)
         IF(KEY3.EQ.0) GOTO 800
C     PRINT FINAL FLUID CONCENTRATIONS
         DO 450 M=1,MMAX
            DO 450 N=1,NMAX
  450    COUT(N,M)=C(N,M)
         CALL PRINTC(COUT,NMAX,MMAX,PARAM(NSP1),ETS,1,ICODE)
         DO 460 M=1,MMAX
            DO 460 N=1,NMAX
  460    COUT(N,M) = TOP(N,M)
         CALL PRINTC(COUT,NMAX,MMAX,PARAM(NSP1),ETS,3,ICODE)
         DO 470 M=1,MMAX
            DO 470 N=1,NMAX
  470    COUT(N,M)=THICK(N,M)
         CALL PRINTC(COUT,NMAX,MMAX,PARAM(NSP1),ETS,4,ICODE)
         WRITE(6,505) TRACER,CINIT,CBACK
  505    FORMAT(1H1/////10X,74HDILUTION TIMES FOR CONSERVATIVE TRACER IN
     1   INITIAL FLUID FRACTION FOLLOW...//10X,10HTRACER IS ,A10,28H,
     2    INITIAL CONCENTRATION IS,G15.7,36H MG/L, BACKGROUND
     3    CONCENTRATION IS,G15.7,5H MG/L )
         ITD=ITD-1
         DO 530 I=1,ITD
            IDL = 1./DC(I) + .001
            WRITE(6,515) IDL,TD(I)
  515       FORMAT(10X,12HDILUTION IS,I6,13H TO 1 WITHIN,F10.3,19H SECONDS
     1       AFTER DUMP)
  530    CONTINUE
         WRITE(6,535)
  535    FORMAT(//10X,54HDILUTION TIMES ARE FOR POINT OF MAXIMUM
     1    CONCENTRATION.)
         GOTO 800
  700    WRITE(6,705)
  705    FORMAT(//49H FIFTH SOLUTION TRIAL NOT SATISFACTORY--CALL EXIT)
         CALL EXIT
  800    CALL SECOND(T2)
         T3=T2-T1
         WRITE(6,805) T3
  805    FORMAT(27H1RUN COMPLETED, CPU TIME =,F7.3,5H SEC,)
         RETURN
      END