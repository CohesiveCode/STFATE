
      SUBROUTINE DUMP(SS,U,W,DEPTH,NS)
C
C     THREE-DIMENSIONAL AXI-SYMMETRIC INSTANTANEOUS RELEASE OF ENTIRE
C     LOAD FROM BARGE
C
         EXTERNAL DERIVD
         DIMENSION SS(600,NS), U(1), W(1), DEPTH(1)
         COMMON/AMB/ NROA,IY,Y(10),ROA(10),H
         COMMON/CLOUD/ T(600),CX(600),CY(600),CZ(600),CU(600),CV(600),
     1    CW(600),DENDIF(600),BC(600),AA(600),FC(600),VF
         COMMON/PIECES/ PARAM(13),ROAS(13),CS(13),VFALL(13),VOIDS(13),
     1    BVOID
         COMMON/GUIDE1/ TDUMP,TSTOP,ISTEP,IPLUNG,NUTRL,NTRIAL,ILEAVE,
     1    KEY1,KEY2,KEY3
         COMMON/GPI/ G,PI
         COMMON/SICOEF/ ALPHA,ALPHAO,ALPHAC,BETA,CDRAG,CFRIC,CD,CD1,CD2,
     1    CD3,CD4,CM,DINCR1,DINCR2,FRICTN,GAMA,F1
         COMMON/LTCOF/ ALAMDA,DIF,AKYO
         COMMON/DTEES/ DT,DT1,DT2
         COMMON/COL/ AO,IBED,FBED
         COMMON/SWITCH/ ITF
         COMMON/GP/ IGCN,IGCL,IGLT,IPCN,IPCL,IPLT
         COMMON/USERDT/ KEY4,DT1U,DT2U
         COMMON/FLEE/ ITD,TD(6),DC(6),TRACER,CINIT,CBACK
         COMMON/DUMP1/ E(22)
         COMMON/BAY/ DX,DTL,XBARGE,ZBARGE,DXH,DXR,AREA
         DIMENSION VORT(600),ACONC(12),SAVE(22)
         NTRIAL=0
C
         KV=1
         IBED=0
         ILEAVE=999
         NSP1=NS+1
C
C
C     ....HERE TO SET INITIAL CONDITIONS....
C
C
         READ(5,25) RB,DREL,CU(1),CV(1),CW(1),ROO,BVOID
         WRITE(6,125) RB,DREL,CU(1),CV(1),CW(1),ROO,BVOID
  125    FORMAT(1H1//10X,23HDISCHARGE PARAMETERS.,10X,
     1    30HINITIAL RADIUS OF CLOUD, RB =,G15.7/10X,
     2    40HINITIAL DEPTH OF CLOUD CENTROID, DREL =,G12.4/10X,
     3    35HINITIAL CLOUD VELOCITIES...CU(1) =,G12.4,3X,
     4    8HCV(1) =,G12.4,3X,8HCW(1) =,G12.4//10X,
     5    18HBULK PARAMETERS.../10X,15HDENSITY, ROO =,G15.7/10X,
     6    31HAGGREGATE VOIDS RATIO, BVOID =,G12.4)
         WRITE(6,135) NS
  135    FORMAT(5(/),10X,10HTHERE ARE,I2,34H SOLIDS, PARAMETERS FOLLOW...
     1    ....//10X,90HDESCRIPTION          DENSITY(GM/CC)
     2    CONCENTRATION(CUFT/CUFT)   FALL VELOCITY(FT/SEC)   VOIDS RATIO/)
         DO 150 K=1,NS
            READ(5,35) PARAM(K),ROAS(K),CS(K),VFALL(K),VOIDS(K)
   35       FORMAT(A10,7E10.0)
            WRITE(6,145) PARAM(K),ROAS(K),CS(K),VFALL(K),VOIDS(K)
  145       FORMAT(11X,A10,2X,G12.4,9X,G12.4,15X,G12.4,5X,G12.4)
  150    CONTINUE
         PARAM(NSP1) = 'FLUID'
         VFALL(NSP1) = 0.
C     NON-ANSI
C     CHECK CONSISTENCY OF PARAMETERS WITH NECESSARY MINIMUM FLUID DENSITY OF 1.
         CS(NSP1)=1.
         CIV = 2.*PI*RB**3/3.
         CIVS=CIV
         CIM=ROO*CIV
         DO 170 K=1,NS
C
            SV=CS(K)*CIVS
            SM=SV*ROAS(K)
            CS(NSP1)=CS(NSP1)-CS(K)
            CIV=CIV-SV
            CIM=CIM-SM
  170    CONTINUE
         FD=CIM/CIV
         WRITE(6,145) PARAM(NSP1),FD,CS(NSP1),VFALL(NSP1)
         IF(FD.GE.0.97) GOTO 190
         WRITE(6,185)
  185    FORMAT(//10X,44HFLUID DENSITY LESS THAN 0.97 GM/CC, CALL EXIT)
         CALL EXIT
  190    CONTINUE
C     READ INFO FOR DILUTION OF CHEMICAL TRACER
         READ(5,192) TRACER,CINIT,CBACK
  192    FORMAT(A10,2E10.0)
C     ....CONVERT UNITS FROM G/CC TO SLUGS/CUFT......
         DO 200 I=1,NS
            ROAS(I)=ROAS(I)*1.94
  200    CONTINUE
         ROO=ROO*1.94
C
         IF(KEY1.EQ.2) GOTO 70
C
C
C     ....HERE TO USE TETRA TECH COEFFICIENTS....
         WRITE(6,65)
C
C
C
C
   65    FORMAT(//////10X,37HUSE TETRA TECH SUGGESTED COEFFICIENTS)
         DINCR1=1.
         DINCR2=1.
         ALPHAO=0.235
         BETA=0.
         CM=1.
         CD=0.5
         CDRAG=1.
         GAMA=0.25
         CFRIC=.01
         CD3=.1
         CD4=1.
         ALPHAC=.001
         FRICTN=.01
         F1=0.1
         ALAMDA=.005
         AKYO=.05
         GOTO 80
C     ....HERE TO READ IN COEFFICIENTS....
   70    WRITE(6,75)
   75    FORMAT(//////10X,24HUSE READ IN COEFFICIENTS)
         READ(5,25) DINCR1,DINCR2
         READ(5,25) ALPHAO,BETA,CM,CD
         READ(5,25) GAMA,CDRAG,CFRIC,CD3,CD4,ALPHAC,FRICTN,F1
         READ(5,25) ALAMDA,AKYO
   25    FORMAT(8E10.0)
C     ....HERE TO WRITE COEFFICIENTS....
   80    WRITE(6,85) DINCR1,DINCR2
   85    FORMAT(10X,6HDINCR1,F10.4,7H DINCR2,F10.4)
         WRITE(6,95) ALPHAO,BETA,CM,CD
   95    FORMAT(10X,6HALPHAO,F10.4,5H BETA,F10.4,3H CM,F10.4,3H CD,F10.4)
         WRITE(6,105) GAMA,CDRAG,CFRIC,CD3,CD4,ALPHAC,FRICTN,F1
  105    FORMAT(10X,4HGAMA,5X,F5.2,3X,5HCDRAG,2X,F5.2,3X,5HCFRIC,1X,F5.3,
     1    2X,3HCD3,2X,F5.2,1X,3HCD4,1X,F5.2,1X,6HALPHAC,F10.4/10X,
     2    6HFRICTN,F10.4,1X,2HF1,F10.4)
         WRITE(6,115) ALAMDA,AKYO
  115    FORMAT(10X,6HALAMDA,F10.4,1X,4HAKYO,F10.4/)
C     ....SAVE AMBIENT DENSITY AT Y(1)....
         ROAA=ROA(1)
         C1=(ROO-ROA(1))/ROA(1)
         E1=(ROA(NROA)-ROA(1))/(H*ROA(1))
         FF=CV(1)/SQRT(G*C1*RB)
         EE1=E1*RB/C1
C
C     TOTAL NUMBER OF EQUATIONS
         NE=9+NS
C
C     LONG TERM DIFFUSION PARAMETER
         DIF = ALAMDA * DX**1.333333*DTL/DX**2
         IF(DIF.GT.0.2) DIF=.2
C
C     ....END OF INITIAL CONDITIONS....
C     ....SELECT TIME STEP FOR INTEGRATIONS....
         IF(ROA(NROA).NE.ROA(1)) GOTO 230
         IF(CV(1).NE.0.) GOTO 220
  210    DT=5.
         GOTO 250
  220    DT=0.01*RB
         GOTO 250
  230    IF(CV(1).EQ.0.) GOTO 210
C     CRITERION FROM CONDITION OF STRATIFICATION
         DT=.01*PI*FF/SQRT(EE1)
C     CRITERION FROM UNIFORM AMBIENT
         DT1 = .01*RB*FF * (1.+ALPHAO*H/RB)**2 / (CV(1)*2.)
         IF(DT-DT1) 250,240,240
  240    DT=DT1
C     INITIAL POSITION OF CLOUD CENTROID (W/R TO BARGE)
  250    E(1)=0.
C
C
C
C
C
C
         E(2)=DREL
         E(3)=0.
C     ....INITIAL MASS OF CLOUD....
         VOLUME = 2.*PI*RB**3/3.
         E(4) = ROO*VOLUME
C     ....INITIAL MOMENTA....
         CMMASS = CM*E(4)
         E(5) = CMMASS*CU(1)
         E(6) = CMMASS*CV(1)
         E(7) = CMMASS*CW(1)
C     ....INITIAL BUOYANCY....
         E(8) = (ROA(1)-ROO)*VOLUME
C     ....INITIAL VORTICITY....
         E(9)=RB*CV(1)*FLOAT(KV)
C     ....SUBTRACT VOLUME OF VARIOUS SOLID COMPONENTS FROM TOTAL WASTE
C     VOLUME, VF, AND PLACE IN E ARRAY. REMAINDER IN VF IS
C     VOLUME OF FLUID WASTE....
         VF = VOLUME
         DO 260 K=1,NS
            E(K+9)=CS(K)*VOLUME
  260    VF = VF-E(K+9)
         DO 270 I=1,NE
  270    SAVE(I)=E(I)
         ROSAV = ROO
         ALPHA = ALPHAO
         DINCR = DINCR1
         WRITE(6,295)
C
C
C
C
C
C
C
C
C
  295    FORMAT(1H1,10X,18HCONVECTIVE DESCENT///10X,
     1    34HCOMPUTATIONAL INDICATORS FOLLOW...//10X,6HNTRIAL,9X,2HDT,
     2    7X,6HIPLUNG,1X,5HNUTRL,1X,5HISTEP)
         IF(KEY4.EQ.1) DT=DT1U
C     ....HERE TO BEGIN A SOLUTION TRIAL....
  400    ROO=ROSAV
C     ....INCREMENT TRIAL NUMBER....
         NTRIAL=NTRIAL+1
C     ....INITIALIZE VARIABLES....
         T(1)=0.
         IY=1
         ISTEP=1
         NUTRL=0
         IPLUNG=0
         DO 410 I=1,NE
  410    E(I)=SAVE(I)
C     ....HERE TO BEGIN COMPUTATIONAL LOOP IN TIME....
C     STORE RESULTS FROM INITIAL CONDITIONS OR PREVIOUS COMPUTATION
C     IN APPROPRIATE ARRAYS....
  420    CX(ISTEP)=E(1)
         CY(ISTEP)=E(2)
         CZ(ISTEP)=E(3)
         VOLUME=(E(4)+E(8))/ROA(1)
         CMMASS = 1./(CM*E(4))
         CU(ISTEP)=E(5)*CMMASS
         CV(ISTEP)=E(6)*CMMASS
         CW(ISTEP)=E(7)*CMMASS
C     WHEN VORTICITY GOES TO ZERO, IT IS SET TO ZERO
         IF(E(9).LE.0.) E(9)=0.
         VORT(ISTEP)=E(9)
C     BC IS DIAMETER OF CLOUD
         AA(ISTEP)=(1.5*VOLUME/PI)**.333333
         BC(ISTEP)=2.*AA(ISTEP)
C     SS IS SOLID CONCENTRATION IN VOLUME RATIO
         DO 430 K=1,NS
  430    SS(ISTEP,K)=E(K+9)/VOLUME
C     FLUID CONCENTRATION
         FC(ISTEP)=VF/VOLUME
         CTRACE=(CINIT*VF+(VOLUME-VF)*CBACK)/VOLUME
         DR=CTRACE/CINIT
         IF(DR.GT.DC(ITD)) GOTO 460
         TD(ITD)=T(ISTEP)
         ITD=ITD+1
  460    CONTINUE
C     NEW OVERALL DENSITY OF WASTE CLOUD
         ROO=E(4)/VOLUME
C     INTERPOLATE FOR AMBIENT DENSITY AT VERTICAL POSITION E(2)
         ROAA = ROA(IY)+(E(2)-Y(IY))*(ROA(IY+1)-ROA(IY))/(Y(IY+1)-Y(IY))
C     CONVERT DENSITY DIFFERENCE BACK TO GM/CC AND STORE IN DENDIF
C
C
C
         DENDIF(ISTEP)=(ROO-ROAA)*.51545
C
C     TEST FOR BOTTOM ENCOUNTER
         IF((CY(ISTEP)+3.*AA(ISTEP)/8.).GE.H) IPLUNG=1
C
C     TEST FOR LOOP EXIT
         IF(IPLUNG.EQ.1) GOTO 500
         IF(NUTRL.EQ.1) GOTO 500
         IF(ISTEP.GE.600) GOTO 500
C
C     SOLVE EQUATION SET FOR NEXT TIME STEP
         CALL RUNGS(DERIVD,NE,U,W,DEPTH)
         ISTEP=ISTEP+1
         T(ISTEP)=T(ISTEP-1)+DT
C
C
C
C
C
C
C
C
         GOTO 420
C     ....END OF LOOP IN TIME.....
C     PRINT OUT VARIABLES GUIDING JUST COMPLETED SOLUTION TRIAL
  500    WRITE(6,505) NTRIAL,DT,IPLUNG,NUTRL,ISTEP
  505    FORMAT(9X,I5,G16.8,2X,3I6)
C     ITF SAVES LAST TIME STEP OF DESCENT PHASE
         ITF=ISTEP
C     TEST FOR PROPER NUMBER OF TIME STEPS IN CONVECTIVE CALCULATIONS
         IF(ISTEP.LT.100.OR.ISTEP.GT.200) GOTO 510
         GOTO 520
  510    DT = DT*FLOAT(ISTEP)*DINCR/150.
C     IF FIFTH TRIAL COMPLETED, GO TO OUTPUT SECTION, IF NOT RETURN FOR
C     NEXT TRIAL
         IF(NTRIAL.GE.5) GOTO 520
         GOTO 400
C     ....HERE FOR PRINTED AND/OR GRAPHICAL OUTPUT....
  520    IF(IPCN.EQ.0) GOTO 600
C     ....HERE FOR PRINTOUT....
         WRITE(6,524)
  524    FORMAT(//10X,42HX AND Z ARE MEASURED W/R TO BARGE POSITION)
         WRITE(6,525)
  525    FORMAT(//8X,4HTIME,5X,1HX,7X,1HY,7X,1HZ,6X,1HU,6X,1HV,5X,1HW,6X,
     1    7HDEN-DIF,3X,6HRADIUS,1X,5HDIA.,2X,5HVORT.,3X,12HFLUID CONC.,
     2    10HSOLID-VOL,2X,13HCONCENTRATION)
         LINC=ISTEP/60
         IF(LINC.LT.1) LINC=1
         DO 560 J=1,ISTEP,LINC
            DO 530 K=1,NS
  530       ACONC(K) = 2.*PI*AA(J)**3*SS(J,K)/3.
            WRITE(6,535) T(J),CX(J),CY(J),CZ(J),CU(J),CV(J),CW(J),DENDIF(J),
     1       AA(J),BC(J),VORT(J),FC(J),ACONC(1),SS(J,1)
  535       FORMAT(4X,4F8.2,F6.2,F7.3,F6.2,E12.4,2F7.2,1X,F7.4,3F12.4)
            IF(NS.EQ.1) GOTO 560
            DO 540 K=2,NS
  540       WRITE(6,545) ACONC(K),SS(J,K)
  545       FORMAT(101X,2F12.4)
  560    CONTINUE
  600    IF(IGCN.EQ.0) GOTO 700
C     ....HERE FOR GRAPHING....
         ISTEP1=ISTEP+1
         T(ISTEP1)=2.*T(ISTEP)-T(ISTEP-1)
         CX(ISTEP1)=2.*CX(ISTEP)-CX(ISTEP-1)
         CZ(ISTEP1)=2.*CZ(ISTEP)-CZ(ISTEP-1)
         CY(ISTEP1)=0.
         AA(ISTEP1)=0.
         FC(ISTEP1)=0.
         CALL DRAW(T,T,T,T,CY,AA,CX,CZ,ISTEP1,1,4)
         IF(IGCN.EQ.1) GOTO 700
         DO 610 I=1,NS
  610    SS(ISTEP1,I)=0.
         NG=4
         IF(NS.LT.4) NG=NS
         CALL DRAW(T,T,T,T,SS(1,1),SS(1,2),SS(1,3),SS(1,4),ISTEP1,3,NG)
         IF(NS.LE.4) GOTO 700
         NG=4
         IF(NS.LT.8) NG=NS-4
         CALL DRAW(T,T,T,T,SS(1,5),SS(1,6),SS(1,7),SS(1,8),ISTEP1,4,NG)
         IF(NS.LE.8) GOTO 700
         NG=4
         IF(NS.LT.12) NG=NS-8
         CALL DRAW(T,T,T,T,SS(1,9),SS(1,10),SS(1,11),SS(1,12),ISTEP1,7,NG)
  700    CONTINUE
C
C
C
C     ....SHIFT DATA TO PREPARE FOR COLLAPSE PHASE....
         DT1=DT
         DO 730 K=1,NS
            I=NS-K
  730    E(I+11)=E(I+10)
         E(9)=AA(ISTEP)
         AO=AA(ISTEP)
         IF(IPLUNG.EQ.1) GOTO 720
         E(10)=0.
         RETURN
C     ....HERE IF CLOUD HAS HIT BOTTOM....
  720    E(10) = ROO*PI*E(9)**3*(2.666666*CV(ISTEP))/32.
         IBED=ISTEP
         RETURN
      END

    !   ------------------------------------------------

      SUBROUTINE DERIVD(E,U,W,DEPTH)
C
C     ....CALLED FROM DUMP VIA RUNGS....
C
         DIMENSION E(22), U(1), W(1), DEPTH(1)
         COMMON/DPASS/ NPASS,MPASS
         COMMON/DIMEN/ NS,NSP1,NVL,NSC
         COMMON/A/ EP(22)
         COMMON/BAY/ DX,DTL,XBARGE,ZBARGE,DXH,DXR,AREA
         COMMON/AMB/ NROA,IY,Y(10),ROA(10),H
         COMMON/PIECES/ PARAM(13),ROAS(13),CS(13),VFALL(13),VOIDS(13),
     1    BVOID
         COMMON/GUIDE1/ TDUMP,TSTOP,ISTEP,IPLUNG,NUTRL,NTRIAL,ILEAVE,
     1    KEY1,KEY2,KEY3
         COMMON/GPI/ G,PI
         COMMON/SICOEF/ ALPHA,ALPHAO,ALPHAC,BETA,CDRAG,CFRIC,CD,CD1,CD2,
     1    CD3,CD4,CM,DINCR1,DINCR2,FRICTN,GAMA,F1
         COMMON/COL/ AO,IBED,FBED
         IF(E(2).GE.0.) GOTO 30
         WRITE(6,15)
   15    FORMAT(1X,51HDEPTH Y .LT. 0--CHANGE INPUT DATA TO ENSURE DESCENT)
         CALL EXIT
C
   30    CONTINUE
C     SET IY SO THAT CLOUD DEPTH E(2) IS BRACKETED BY Y(IY) AND Y(IY+1)
         IF(E(2).LE.Y(IY+1)) GOTO 40
         IY=IY+1
         GOTO 30
   40    IF(E(2)-Y(IY)) 50,100,100
C
   50    IY=IY-1
         GOTO 30
C     INTERPOLATE FOR AMBIENT DENSITY AT DEPTH OF CENTROID OF CLOUD...
  100    ROAA = ROA(IY)+(E(2)-Y(IY))*(ROA(IY+1)-ROA(IY))/(Y(IY+1)-Y(IY))
         CE = (ROA(IY+1)-ROA(IY))/(Y(IY+1)-Y(IY))
         VOLUME=(E(4)+E(8))/ROA(1)
         ROO=E(4)/VOLUME
         IF(ROO.LE.ROAA) NUTRL=1
         B=(1.5*VOLUME/PI)**.333333
C
C
         XX=XBARGE+E(1)
C
C     DETERMINE HORIZONTAL VELOCITIES AT CLOUD
         ZZ=ZBARGE+E(3)
         CALL VEL(XX,E(2),ZZ,UA,WA,U,W,DEPTH,NPASS,MPASS)
         IF(E(9)) 110,110,120
  110    ALPHA=ALPHAO
         GOTO 200
  120    ALPHA=ALPHAO*SQRT(TANH((G*VOLUME*(ROO-ROAA)/(2.*PI*.16*ROO*
     1    E(9)**2*ALPHAO))**2))
C
C
C     MAIN COMPUTATIONS
  200    CMMASS=CM*E(4)
         UU=E(5)/CMMASS
         VV=E(6)/CMMASS
         WW=E(7)/CMMASS
         PHI=SQRT((UU-UA)**2+VV**2+(WW-WA)**2)
C     ENTRAINED VOLUME IS...
         ENTRV = 2.*PI*B**2*ALPHA*PHI
         EP(1)=UU
         EP(2)=VV
         EP(3)=WW
         EP(4)=ENTRV*ROAA
         DRAG=CD*ROAA*PI*B**2*PHI*.5
         EP(5)=ENTRV*ROAA*UA - DRAG*(UU-UA)*.5
         EP(6)=VOLUME*(ROO-ROAA)*G - DRAG*VV
         EP(7)=ENTRV*ROAA*WA - DRAG*(WW-WA)*.5
         EP(8)=ENTRV*(ROA(1)-ROAA)
         EP(9)=-3.*B**2*G*CE/ROA(1)
         DO 250 K=1,NS
            ABSWS=ABS(VFALL(K))
C     IF FALL VEL IS SMALLER THAN THE CONVECTIVE VEL. NO SETTLING OCCURS
            IF(ABSWS-ABS(VV)) 220,220,230
  220       BETAA=1.
            GOTO 240
  230       BETAA=BETA
  240       SETLV=PI*B**2*ABS(VFALL(K))*(1.-BETAA)*E(K+9)/VOLUME
            EP(4)=EP(4)-SETLV*(ROAS(K))
            EP(5)=EP(5)-SETLV*(ROAS(K))*UU
            EP(6)=EP(6)-SETLV*(ROAS(K))*VV
            EP(7)=EP(7)-SETLV*(ROAS(K))*WW
            EP(8)=EP(8)-SETLV*(ROA(1)-ROAS(K))
            EP(K+9)=-SETLV
  250    CONTINUE
         RETURN
      END
      SUBROUTINE COLAPS(SS,U,W,DEPTH,NS)
         EXTERNAL DERIVC
         DIMENSION SS(600,NS), U(1), W(1), DEPTH(1)
         COMMON/AMB/ NROA,IY,Y(10),ROA(10),H
         COMMON/CLOUD/ T(600),CX(600),CY(600),CZ(600),CU(600),CV(600),
     1    CW(600),DENDIF(600),BC(600),AA(600),FC(600),VF
         COMMON/PIECES/ PARAM(13),ROAS(13),CS(13),VFALL(13),VOIDS(13),
     1    BVOID
         COMMON/GUIDE1/ TDUMP,TSTOP,ISTEP,IPLUNG,NUTRL,NTRIAL,ILEAVE,
     1    KEY1,KEY2,KEY3
         COMMON/COL/ AO,IBED,FBED
         COMMON/DUMP1/ E(22)
         COMMON/GPI/ G,PI
         COMMON/SICOEF/ ALPHA,ALPHAO,ALPHAC,BETA,CDRAG,CFRIC,CD,CD1,CD2,
     1    CD3,CD4,CM,DINCR1,DINCR2,FRICTN,GAMA,F1
         COMMON/LTCOF/ ALAMDA,DIF,AKYO
         COMMON/DTEES/ DT,DT1,DT2
         COMMON/GP/ IGCN,IGCL,IGLT,IPCN,IPCL,IPLT
         COMMON/USERDT/ KEY4,DT1U,DT2U
         COMMON/FLEE/ ITD,TD(6),DC(6),TRACER,CINIT,CBACK
         DIMENSION SAVE(22),ACONC(12)
         DINCR=DINCR2
         NTRIAL=0
         ISAV=ISTEP
         IF(ISTEP.EQ.IBED) GOTO 10
C     ..HERE IF CLOUD HAS NOT ENCOUNTERED BOTTOM....
         E1=(ROA(IY+1)-ROA(IY))/(ROA(1)*(Y(IY+1)-Y(IY)))
         EG=SQRT(E1*G)
         B1=(AA(ISTEP)**3*.84*EG/1000.)**.42857
         DT2=.001*(B1/AA(ISTEP))**3/EG*.1
         DT=DT2
         GOTO 20
C     ....HERE IF CLOUD IS ON BOTTOM....
   10    DT=DT1
   20    WRITE(6,25)
   25    FORMAT(1H1,10X,23HCOLLAPSE PHASE OF CLOUD///10X,
     1    27HCOMPUTATIONAL INDICATORS.../5X,6HNTRIAL,4X,2HDT,6X,6HIPLUNG
     2    ,2X,5HNUTRL,2X,5HISTEP,2X,4HIBED,3X,6HILEAVE)
         IF(KEY4.EQ.1) DT=DT2U
         NE=NS+10
C     SAVE STARTING VALUES IN E ARRAY...
         DO 100 KK=1,NE
  100    SAVE(KK)=E(KK)
C     ....HERE TO BEGIN A TRIAL....
  400    DO 410 KK=1,NE
  410    E(KK)=SAVE(KK)
         NTRIAL=NTRIAL+1
         ISTEP=ISAV
         VOLUME=(E(4)+E(8))/ROA(1)
         ROO=E(4)/VOLUME
         NUTRL=0
         IPLUNG=0
         IF(ISTEP.EQ.IBED) IPLUNG=1
C
C     IF CLOUD HAS HIT BOTTOM, GO DIRECTLY TO BOTTOM
         IF(ISTEP.EQ.IBED) GOTO 520
C
C     IF THIS IS FIRST TIME STEP IN COLAPS, GO DIRECTLY TO RUNGS ROUTINE.
         IF(ISTEP.EQ.ISAV) GOTO 460
C
C     ....HERE TO BEGIN MAIN COMPUTATIONAL LOOP IN TIME....
C
C     ....SAVE RESULTS OF PREVIOUS COMPUTATIONS....
  420    CX(ISTEP)=E(1)
         CY(ISTEP)=E(2)
C
C
C
C
         CZ(ISTEP)=E(3)
         VOLUME=(E(4)+E(8))/ROA(1)
         CMMASS=CM*E(4)
C     E(9) IS SEMI-MAJOR AXIS
C     BC IS HORIZONTAL DIMENSION OF CLOUD
         BC(ISTEP)=2.*E(9)
C     AA IS VERTICAL DIMENSION OF CLOUD
         AA(ISTEP)=6.*VOLUME/(PI*BC(ISTEP)**2)
         CU(ISTEP)=E(5)/CMMASS
         CV(ISTEP)=E(6)/(CMMASS*BC(ISTEP))*AA(ISTEP)
         CW(ISTEP)=E(7)/CMMASS
         ROO=E(4)/VOLUME
         ROAA=ROA(IY)+(E(2)-Y(IY))*(ROA(IY+1)-ROA(IY))/(Y(IY+1)-Y(IY))
         DENDIF(ISTEP)=(ROO-ROAA)*.51545
C     SS SAVE SOLID CONCENTRATION FOR DIFFUSION
         DO 430 K=1,NS
  430    SS(ISTEP,K)=E(K+10)/VOLUME
         FC(ISTEP)=VF/VOLUME
         CTRACE=(CINIT*VF+(VOLUME-VF)*CBACK)/VOLUME
         DR=CTRACE/CINIT
         IF(DR.GT.DC(ITD)) GOTO 432
         TD(ITD)=T(ISTEP)
         ITD=ITD+1
  432    CONTINUE
         IF(IPLUNG.EQ.4) GOTO 440
         IF((CY(ISTEP)+3.*AA(ISTEP)/8.).GE.H) IPLUNG=2
C
C     AKX CHANGE OF B
C     BY DIFFUSION
  440    AKX=2.*ALAMDA*(E(9))**.333333
C
C
C
         DBDT=.5*(BC(ISTEP)-BC(ISTEP-1))/DT
C     ....EXIT TESTS....
C     IF CLOUD TOUCHES FREE SURFACE, EXIT TO PRINTOUTS...
         IF(CY(ISTEP)-AA(ISTEP).LE.0.) GOTO 570
         IF(ISTEP.LE.ISAV+5) GOTO 450
C     IF CHANGE OF CLOUD MAJOR AXIS BY DIFFUSION IS GT.
C     OR EQUAL TO CHANGE IN MAJOR AXIS IN ONE TIME STEP, ATTEMPT EXIT TO BEGIN
C     LONG TERM DIFFUSION....
         IF(AKX.GE.DBDT) NUTRL=3
  450    CONTINUE
         IF(NUTRL.EQ.3) GOTO 550
C
C     IF CLOUD HIT BOTTOM WHILE COLLAPSING GO CALL BOTTOM
         IF(IPLUNG.EQ.2) GOTO 500
         IF(ISTEP.GE.599) GOTO 550
  460    CONTINUE
         ISTEP=ISTEP+1
         CALL RUNGS(DERIVC,NE,U,W,DEPTH)
         T(ISTEP)=T(ISTEP-1)+DT
         GOTO 420
C     ....END OF MAJOR LOOP....
C     ....HERE TO COMPUTE COLLAPSE ON BOTTOM....
  500    IBED=ISTEP
         DBT = E(10)*16./(PI*.5*AA(ISTEP)*.25*BC(ISTEP)**2*ROO)
         E(10)=ROO*PI*.5*AA(ISTEP)*.25*BC(ISTEP)**2
     1    *(DBT+.6666667*.5*BC(ISTEP)*CV(ISTEP)/(.5*AA(ISTEP)))/8.
  520    CALL BOTTOM(SS,U,W,DEPTH,NS,NEXT)
         GOTO (530,540,550),NEXT
  530    E(6)=CM*E(4)*CV(ISTEP)
         E(2)=CY(ISTEP)
         DBT=E(10)*8./(PI*AA(ISTEP)*.25*BC(ISTEP)**2*ROO)
C
C
C
         E(10)=ROO*PI*AA(ISTEP)*.50*BC(ISTEP)**2*DBT/16.
         ILEAVE=ISTEP
C     GO CALL RUNGS
         GOTO 460
  540    ISTEP=ISTEP-1
  550    WRITE(6,555) NTRIAL,DT,IPLUNG,NUTRL,ISTEP,IBED,ILEAVE
  555    FORMAT(5X,I4,G12.4,I6,4I7)
         IF((ISTEP-ISAV).LE.100.OR.(ISTEP-ISAV).GE.399) GOTO 560
         GOTO 570
  560    DT=DT*FLOAT(ISTEP-ISAV)*DINCR/250.
         IF(NTRIAL.GE.5) GOTO 570
C     RETURN FOR ANOTHER TRIAL
         GOTO 400
C     ....HERE FOR PRINTOUT AFTER COMPUTATIONS COMPLETED....
  570    IF(IPCL.EQ.0) GOTO 600
         WRITE(6,574)
  574    FORMAT(//10X,36HX AND Z MEASURED FROM BARGE POSITION)
         WRITE(6,575)
  575    FORMAT(/6X,4HTIME,8X,1HX,6X,1HY,6X,1HZ,9X,1HU,6X,1HV,5X,1HW,
     1    6X,7HDEN-DIF,6X,2HAA,4X,2HBC,8X,11HFLUID CONC.,3X,
     2    10HSOLID-VOL,2X,13HCONCENTRATION)
         NGRID=(ISTEP-ISAV)/60
         IF(NGRID.LT.1) NGRID=1
         DO 599 J=ISAV,ISTEP,NGRID
            DO 580 K=1,NS
  580       ACONC(K)=4.*PI*.5*AA(J)*.25*BC(J)**2*SS(J,K)/3.
            WRITE(6,585) T(J),CX(J),CY(J),CZ(J),CU(J),CV(J),CW(J),DENDIF(J),
     1       AA(J),BC(J),FC(J),ACONC(1),SS(J,1)
  585       FORMAT(1X,2F10.2,F8.2,G11.4,F6.2,F7.3,F6.2,E12.4,F7.2,G11.4,
     1       G12.4,E12.4,E12.4)
            IF(NS.EQ.1) GOTO 599
            DO 590 K=2,NS
  590       WRITE(6,595) ACONC(K),SS(J,K)
  595       FORMAT(101X,2F12.4)
  599    CONTINUE
  600    IF(IGCL.EQ.0) GOTO 700
         ISTEP1=ISTEP+1
         T(ISTEP1)=2.*T(ISTEP)-T(ISTEP-1)
         AA(ISTEP1)=0.
         CY(ISTEP1)=0.
         BC(ISTEP1)=0.
         FC(ISTEP1)=0.
         CX(ISTEP1)=2.*CX(ISTEP)-CX(ISTEP-1)
         CZ(ISTEP1)=2.*CZ(ISTEP)-CZ(ISTEP-1)
         CALL DRAW(T,T,T,T,AA,BC,FC,CY,ISTEP1,2,4)
         CALL DRAW(CX,T,T,T,CZ,T,T,T,ISTEP1,5,1)
  700    DT2=DT
         RETURN
      END